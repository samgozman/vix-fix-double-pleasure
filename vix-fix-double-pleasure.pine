//@version=4
// 
// VIX Fix Double Pleasure by @samgozman (https://github.com/samgozman)
// 
// How to use this script: https://github.com/samgozman/vix-fix-double-pleasure
// 
// ++++++++++ Warning: The script is provided for educational purposes only. ++++++++++ //

study(title="VIX Fix: Double Pleasure", overlay=false)

// ++++++++++ INPUTS ++++++++++ //
VIXFix_length = input(22, title="VIX Fix Length")
VIXFix_back = input(22, title="The number of periods among which to look for lows and highs")
plotMarks = input(title="Plot high/low marks", type=input.bool, defval=true)

// ++++++++++ LOGIC ++++++++++ //
// Vix current
VIXFix_classic = (highest(close, VIXFix_length) - low) / highest(close, VIXFix_length) * -100
VIXFix_reverse = (lowest(close, VIXFix_length) - high) / lowest(close, VIXFix_length) * -100

// Minimaxis
isMaxUp = highest(VIXFix_reverse, VIXFix_back) == VIXFix_reverse // Top peaks
isMaxDown = lowest(VIXFix_classic, VIXFix_back) == VIXFix_classic // Top bottoms
isMinUp = lowest(VIXFix_reverse, VIXFix_back) == VIXFix_reverse // Min peaks
isMinDown = highest(VIXFix_classic, VIXFix_back) == VIXFix_classic // Min bottoms

// ++++++++++ PLOT ++++++++++ //
plot(series=VIXFix_reverse, style=plot.style_histogram, linewidth=4, color=color.new(color.teal, 20))
plot(series=VIXFix_reverse, style=plot.style_line, linewidth=4, color=color.new(color.teal, 20), title="VIX fix reversed")

plot(series=VIXFix_classic, style=plot.style_histogram, linewidth=4, color=color.new(color.red, 20))
plot(series=VIXFix_classic, style=plot.style_line, linewidth=4, color=color.new(color.red, 20), title="VIX fix classic")

// ++++++++++ PLOT - Marks ++++++++++ //
plotshape((isMinDown or isMaxUp) and plotMarks, title="Sell", color = color.red, style=shape.circle, location=location.top, size=size.tiny)
plotshape((isMaxDown or isMinUp) and plotMarks, title="Buy", color = color.green, style=shape.circle, location=location.bottom, size=size.tiny)

// ================== TODO ================== //
// Plot Stoch-like lines based on VIX
